Tech
    name = "SPY_DETECT_2"
    description = "SPY_DETECT_2_DESC"
    short_description = "DETECTION_SHORT_DESC"
    category = "SPY_CATEGORY"
    researchcost = 50 * [[TECH_COST_MULTIPLIER]]
    researchturns = 5
    tags = [ "PEDIA_SPY_CATEGORY" ]
    prerequisites = "SHP_GAL_EXPLO"
    unlock = [
        Item type = ShipPart name = "DT_DETECTOR_2"
        Item type = Building name = "BLD_SCANNING_FACILITY"
    ]
    effectsgroups = [
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
            ]
            activation = And [
                [[NOT_OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_3)]]
                [[NOT_OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_4)]]
                [[NOT_OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_5)]]
                [[OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_2)]]
            ]
            effects = SetDetection value = Value + 75

        EffectsGroup
            scope = Source
            activation = And [
                [[NOT_OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_3)]]
                [[NOT_OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_4)]]
                [[NOT_OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_5)]]
                [[OWNER_HAS_TECH_EFFECTIVE(SPY_DETECT_2)]]
            ]
            effects = SetEmpireMeter empire = Source.Owner meter = "METER_DETECTION_STRENGTH" value = Value + 30

        EffectsGroup
            scope = And [
                Capital
                Not OwnedBy empire = Source.Owner
                Number low = 1 condition = And [
                    OwnedBy empire = Source.Owner
                    VisibleToEmpire empire = RootCandidate.Owner
                ]
            ]
            activation = (CurrentTurn < 2 + TurnTechResearched empire = Source.Owner name = "SPY_DETECT_2")
            effects = GenerateSitRepMessage
                message = "SITREP_EMPIRE_TECH_RESEARCHED_DETECTION"
                label = "SITREP_EMPIRE_TECH_RESEARCHED_DETECTION_LABEL"
                icon = "icons/tech/active_radar.png"
                parameters = [
                    tag = "empire"  data = Source.Owner
                ]
                empire = Target.Owner

    ]
    graphic = "icons/tech/active_radar.png"

#include "/scripting/common/base_prod.macros"
#include "/scripting/techs/techs.macros"

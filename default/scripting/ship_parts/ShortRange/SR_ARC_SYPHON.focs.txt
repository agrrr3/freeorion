Part
    name = "SR_ARC_SYPHON"
    description = "SR_ARC_SYPHON_DESC"
    class = ShortRange
    damage = 4
    shots = 0
    mountableSlotTypes = Core
    buildcost = 40 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 1
    tags = [ "PEDIA_PC_DIRECT_WEAPON" ]
    location = And [
        Planet
        OwnedBy empire = Source.Owner
    ]
    effectsgroups = [
        // syphon all arcs in the same fleet
        // once: first collect arc disruptor shot count as a special on the lowest syphon ship id
        EffectsGroup
            scope = Source
            activation = And [
                Source
                [[THE_BOOKKEEPING_ARC_SYPHON_SHIP_IN_SOURCE_FLEET]]
            ]
            stackinggroup = "SR_ARC_SYPHON_CHARGE_STACK"
            effects = [
               SetSpecialCapacity
                name = "SR_ARC_SYPHON_CHARGE"
                capacity = Statistic Sum value = ((ShipPartMeter part = "SR_ARC_DISRUPTOR" meter = SecondaryStat object = LocalCandidate.ID) * (PartsInShipDesign name = "SR_ARC_DISRUPTOR" design = LocalCandidate.DesignID))
                           condition = [[ALL_ARC_DISRUPTOR_SHIPS_IN_SOURCE_FLEET]]
               GenerateSitRepMessage
                    message = "SITREP_SR_ARC_SYPHON_DEBUG"
                    label = "SITREP_SR_ARC_SYPHON_DEBUG_LABEL"
                    icon = "icons/ship_parts/pulse-laser.png"
                    parameters = [
                        tag = "syphoned" data = Statistic Sum value = ((ShipPartMeter part = "SR_ARC_DISRUPTOR" meter = SecondaryStat object = LocalCandidate.ID) * (PartsInShipDesign name = "SR_ARC_DISRUPTOR" design = LocalCandidate.DesignID)) condition = [[ALL_ARC_DISRUPTOR_SHIPS_IN_SOURCE_FLEET]]
                        tag = "shipdesign" data = Source.DesignID
                        tag = "ship" data = Source.ID
                        tag = "fleet" data = Source.FleetID
                    ]
                    empire = Source.Owner
            ]


        // TODO distribute to syphons in this fleet, at a later priority
        // TODO test
        // TODO need to handle fractions better
        EffectsGroup
            scope = [[ALL_ARC_SIPHON_SHIPS_IN_SOURCE_FLEET]]
            activation = And [
                Source
                [[THE_BOOKKEEPING_ARC_SYPHON_SHIP_IN_SOURCE_FLEET]]
            ]
            effects = SetSecondaryStat partname = "SR_ARC_DISRUPTOR" value = ((SpecialCapacity name = "SR_ARC_SYPHON_CHARGE" object = Source.ID) /
            (Statistic Count condition = [[ALL_ARC_SIPHON_SHIPS_IN_SOURCE_FLEET]] ))

        // TODO timing; e.g. with the policies
        // then nullify all capacity of arc disruptors
        EffectsGroup
            scope = [[ALL_ARC_DISRUPTOR_SHIPS_IN_SOURCE_FLEET]]
            activation = And [
                Object id = Source.FleetID
                Fleet
                Contains condition = And [
                    Ship
                    DesignHasPart low = 1 high = 999 name = "SR_ARC_DISRUPTOR"
                ]
            ]
            stackinggroup = "SR_ARC_SYPHON_STACK"
            effects = [
                SetSecondaryStat partname = "SR_ARC_DISRUPTOR" value = 0
                GenerateSitRepMessage
                    message = "SITREP_SR_ARC_SYPHON_NULLIFY"
                    label = "SITREP_SR_ARC_SYPHON_NULLIFY_LABEL"
                    icon = "icons/ship_parts/pulse-laser.png"
                    parameters = [
                        tag = "shipdesign" data = Source.DesignID
                        tag = "ship" data = Source.ID
                        tag = "fleet" data = Source.FleetID
                    ]
                    empire = Source.Owner
            ]
    ]
    icon = "icons/ship_parts/pulse-laser.png"

THE_BOOKKEEPING_ARC_SYPHON_SHIP_IN_SOURCE_FLEET
'''
                //THE_BOOKKEEPING_ARC_SYPHON_SHIP_IN_SOURCE_FLEET
                Object id = Statistic Min value = LocalCandidate.ID condition = And [
                    Ship
                    ContainedBy condition = Object id = Source.FleetID
                    DesignHasPart low = 1 high = 999 name = "SR_ARC_SYPHON"
                ]
'''

ALL_ARC_SIPHON_SHIPS_IN_SOURCE_FLEET
'''
            And [
                // ALL_ARC_SYPHON_SHIPS_IN_SOURCE_FLEET
                Ship
                ContainedBy condition = Object id = Source.FleetID
                DesignHasPart low = 1 high = 999 name = "SR_ARC_SYPHON"
                // end ALL_ARC_SIPHON_SHIPS_IN_SOURCE_FLEET
            ]
'''

ALL_ARC_DISRUPTOR_SHIPS_IN_SOURCE_FLEET
'''
            And [
                // ALL_ARC_DISRUPTOR_SHIPS_IN_SOURCE_FLEET
                Ship
                ContainedBy condition = Object id = Source.FleetID
                DesignHasPart low = 1 high = 999 name = "SR_ARC_DISRUPTOR"
                // end ALL_ARC_DISRUPTOR_SHIPS_IN_SOURCE_FLEET
            ]
'''

#include "shortrange.macros"

#include "/scripting/macros/upkeep.macros"
#include "/scripting/ship_parts/targeting.macros"

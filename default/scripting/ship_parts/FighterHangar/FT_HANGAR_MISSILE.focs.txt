Part
    name = "FT_HANGAR_MISSILE"
    description = "FT_HANGAR_MISSILE_DESC"
    exclusions = [ "FT_HANGAR_0" "FT_HANGAR_1" "FT_HANGAR_2" "FT_HANGAR_3" "FT_HANGAR_4" "FT_HANGAR_MISSILE" "FT_BAY_1" ]
    class = FighterHangar
    // Capacity is set to launch capacity per launch bay (4)
    // "Real" missile capacity is four times launch capacity plus two times launch capacity per bay (8 per launch bay)
    // Default value could be expecting a single launch bay
    capacity = 0
    damage = 1
    NoDefaultCapacityEffect
    combatTargets = And [
        (CombatBout == 3)
        [[COMBAT_TARGETS_VISIBLE_ENEMY]]
        Or [
            [[COMBAT_TARGETS_NOT_DESTROYED_SHIP]]
            Fighter
        ]
    ]
    mountableSlotTypes = Internal
    buildcost = 20 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 1
    tags = [ "PEDIA_PC_FIGHTER_HANGAR" ]
    location = OwnedBy empire = Source.Owner
    effectsgroups = [
        EffectsGroup
            scope = Source
            activation = (Source.LastTurnActiveInBattle == CurrentTurn)
            stackinggroup = "SET_MISSILE_STORAGE"
            accountinglabel = "MISSILE_STORAGE_USE"
            effects = [
              SetMaxCapacity partname = "FT_HANGAR_MISSILE"           value = Max(0, ( SpecialCapacity  name = "MISSILE_STORAGE_CAPACITY" object = Source.ID ) - ( [[MISSILE_LAUNCHES]] ) )
              SetSpecialCapacity name = "MISSILE_STORAGE_CAPACITY" capacity = Max(0, ( SpecialCapacity  name = "MISSILE_STORAGE_CAPACITY" object = Source.ID ) - ( [[MISSILE_LAUNCHES]] ) )
            ]

       EffectsGroup
            scope = Source
            activation = Or [ Not HasSpecial name = "MISSILE_STORAGE_CAPACITY" And [
                InSystem
                Stationary
                (Source.LastTurnActiveInBattle < CurrentTurn)
                // For allied supply needs to be moved to a tech i guess
                ResupplyableBy empire = Source.Owner
            ] ]
            stackinggroup = "SET_MISSILE_STORAGE"
            accountinglabel = "MISSILE_STORAGE_RESUPPLY_LABEL"
            effects = [
                SetSpecialCapacity name = "MISSILE_STORAGE_CAPACITY" capacity = 2 + [[MISSILE_LAUNCHES]]
                SetMaxCapacity partname = "FT_HANGAR_MISSILE"           value = 2 + [[MISSILE_LAUNCHES]]
            ]

       EffectsGroup
            scope = Source
            activation = Source
            stackinggroup = "SET_MISSILE_STORAGE"
            accountinglabel = "MISSILE_STORAGE_FALLBACK_LABEL"
            effects = [
                SetMaxCapacity partname = "FT_HANGAR_MISSILE" value = ( SpecialCapacity  name = "MISSILE_STORAGE_CAPACITY" object = Source.ID )
            ]

        EffectsGroup
            scope = And [
                Source
            ]
            activation = Source
            stackinggroup = "SET_MISSILE_LAUNCH_CAPACITY"
            effects = [
                // Launchable missiles
                SetCapacity partname = "FT_HANGAR_MISSILE" value = [[MISSILE_LAUNCHES]]
            ]
    ]

    icon = "icons/ship_parts/nuclear_missile.png"

MISSILE_LAUNCHES
'''4 * PartsInShipDesign name = "FT_BAY_MISSILE" design = Source.DesignID'''

#include "/scripting/common/upkeep.macros"
#include "/scripting/techs/techs.macros"
#include "/scripting/ship_parts/targeting.macros"

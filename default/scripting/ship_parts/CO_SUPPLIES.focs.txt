// File freeorion/scripting/ship_parts/CO_SUPPLIES.focs.txt

    //  Part which unloads and scraps the ship at planets with BLD_UNLOAD_SUPPLY
    //  Implementation notes: distributes the supplies evenly between all planets with BLD_UNLOAD_SUPPLY.
    //  Different capacities according to slot type: Internal 10 External 30 
    //       Calculated so cheapest hull equipped with supply parts has about 10% extra cost

[[PART_CO_SUPPLIES_FOR_GIVEN_CAPACITY_AND_SLOT_TYPES(010,Internal)]]

[[PART_CO_SUPPLIES_FOR_GIVEN_CAPACITY_AND_SLOT_TYPES(030,External)]]

    // BDD style Matrixtests:
    //  == UNLOADED_SUPPLY_SHIPS_GET_SCRAPPED ==
    //  Given a ship with a ship part CO_SUPPLIES_030
    //  orGiven two ships with a ship part CO_SUPPLIES_030
    //  orGiven a ship with two (or many) ship parts CO_SUPPLIES_030
    //  when entering a system with a planet with BLD_UNLOAD_SUPPLY	
    //  orWhen staying in a system with a planet with BLD_UNLOAD_SUPPLY	
    //  orWhen entering a system with two (or many) planets with BLD_UNLOAD_SUPPLY	
    //  then it is scrapped
    //
    //  == SUPPLY_SHIPS_ONLY_GET_UNLOADED_WITH_BLD_UNLOAD_SUPPLY ==
    //  Given a ship with a ship part CO_SUPPLIES_030
    //  when entering a system without a planet with BLD_UNLOAD_SUPPLY
    //  orWhen being outside of a system and but a planet with BLD_UNLOAD_SUPPLY exists
    //  orWhen staying in a system without a planet with BLD_UNLOAD_SUPPLY but a planet with BLD_UNLOAD_SUPPLY exists
    //  *orWhen staying in a system with a planet with BLD_UNLOAD_SUPPLY but the building is destroyed
    //  orWhen staying in a system with a planet with BLD_UNLOAD_SUPPLY but different owner
    //  then none of the CO_SUPPLIES_030 effects is triggered
    //
    //  ship is unloaded in other system -> capacity doesnt change
    //  ATM *destroying the building when the ship is there in order to stop the effect doesnt work  

PART_CO_SUPPLIES_FOR_GIVEN_CAPACITY_AND_SLOT_TYPES
'''
Part
    name = "CO_SUPPLIES_@1@"
    description = "CO_SUPPLIES_DESC" 
    class = General
    capacity = @1@
    mountableSlotTypes = @2@
    buildcost = @1@
    buildtime = 1
    location = OwnedBy empire = Source.Owner
    effectsgroups = [ [[EFFECTSGROUPS_CO_SUPPLIES_FOR_GIVEN_CAPACITY(@1@)]] ]
    icon = "icons/meter/supply.png"
'''

EFFECTSGROUPS_CO_SUPPLIES_FOR_GIVEN_CAPACITY
'''
        EffectsGroup // for unloading supplies to all eligible planets   
            scope = And [
                Planet
                [[CONDITION_PLANET_IN_SYSTEM_READY_FOR_UNLOAD(Source.SystemID)]]
            ]
            activation = InSystem
            effects = [
//                // Message for testing
//                GenerateSitRepMessage
//                    message = "EFFECT_SUPPLY_UNLOAD_PART CO_SUPPLIES_@1@: %rawtext% supplies from %ship% at %system%."
//                    label = "EFFECT_SUPPLY_UNLOAD_PART_LABEL"
//                    icon = "icons/meter/supply.png"
//                    parameters = [
//                        tag = "system" data = Target.SystemID
//                        tag = "rawtext" data = (PartCapacity name = "CO_SUPPLIES_@1@")
//                        tag = "ship" data = Source.ID
//                    ]
//                    empire = Target.Owner
                AddSpecial name = "UNLOADING_SUPPLIES_SPECIAL"
                SetSpecialCapacity name = "UNLOADING_SUPPLIES_SPECIAL" 
                    capacity = Value + ( (PartCapacity name = "CO_SUPPLIES_@1@") / [[COUNT_PLANETS_WITH_UNLOAD_BUILDING(Source.SystemID)]] )
            ]
        EffectsGroup // for removing empty ship
            scope = And [
                Source
                InSystem
            ]
            activation = And [ 
                InSystem
                Number low = 1 condition = [[CONDITION_PLANET_IN_SYSTEM_READY_FOR_UNLOAD(Source.SystemID)]]
            ]
            stackinggroup = "STACKING_SCRAP_EMPTY_SHIP"
            effects = [
                GenerateSitRepMessage
                    message = "EFFECT_SUPPLY_SCRAP_EMPTY_SHIP"
                    label = "EFFECT_SUPPLY_SCRAP_EMPTY_SHIP_LABEL"
                    icon = "icons/meter/supply.png"
                    parameters = [
                        tag = "system" data = Target.SystemID
                        tag = "rawtext" data = [[SUM_OF_SHIP_SUPPLIES]]
                        tag = "ship" data = Source.ID
                    ]
                    empire = Target.Owner
                Destroy
            ]
'''

SUM_OF_SHIP_SUPPLIES
'''[[SUM_OF_SHIP_SUPPLIES_FOR_PART(CO_SUPPLIES_010)]] + [[SUM_OF_SHIP_SUPPLIES_FOR_PART(CO_SUPPLIES_030)]]'''

SUM_OF_SHIP_SUPPLIES_FOR_PART
'''(PartCapacity name = "@1@") * (PartsInShipDesign Name = "@1@" design = Source.DesignID)'''

COUNT_PLANETS_WITH_UNLOAD_BUILDING
'''( Count condition = And [
                        Planet
                        InSystem id = @1@ 
                        Contains Building name = "BLD_UNLOAD_SUPPLY"
                        ] )'''

CONDITION_PLANET_IN_SYSTEM_READY_FOR_UNLOAD
'''
And [
InSystem id = @1@
Contains Building name = "BLD_UNLOAD_SUPPLY"
OwnedBy empire = Source.Owner
]
'''

Policy
    name = "PLC_CONTINUOUS_SCANNING"
    description = "PLC_CONTINUOUS_SCANNING_DESC"
    short_description = "PLC_CONTINUOUS_SCANNING_SHORT_DESC"
    category = "MILITARY_CATEGORY"
    adoptioncost = 5 + [[SHIPS_OWNED_BY_EMPIRE]]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

         // first five turns increase detection
         EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            effects = SetEmpireMeter
                empire = Source.Owner
                meter = "METER_DETECTION_STRENGTH"
                value = Value + min((NamedReal name = "CONTINUOUS_SCANNING_MAX_DETECTION_BOOST" value = 5.0),
                                    TurnsSincePolicyAdopted empire = Source.Owner name = ThisPolicy)

        // then start increasing unstealth
        EffectsGroup
            scope = And [
                Or [
                   Ship
                   And [ Planet    Or [ Species   OwnedBy affiliation = AnyEmpire ] ]
                ]

                Not Or [
                    OwnedBy empire = Source.Owner
                    OwnedBy affiliation = AllyOf empire = Source.Owner
                ]
                (RootCandidate.Unstealth < (NamedReal name = "CONTINUOUS_SCANNING_MAX_UNSTEALTH" value = 10.0))
                // TODO base this actually on detection range of Source.Owner ships
                VisibleToEmpire empire = Source.Owner
            ]
            activation = And [
                ( NamedRealLookup name = "CONTINUOUS_SCANNING_MAX_DETECTION_BOOST"
                  < TurnsSincePolicyAdopted empire = Source.Owner name = ThisPolicy )
            ]
            stackinggroup = "CONTINUOUS_SCANNING_ADD_UNSTEALTH_STACK"
            accountinglabel = "CONTINUOUS_SCANNING_ADD_UNSTEALTH_LABEL"
            effects = [
                SetUnstealth value = Value + 1.01
                GenerateSitRepMessage
                    message = "Add unstealth to sh:%ship% pl:%planet% by %empire%  %rawtext:unst% -> %rawtext:crunst%:"
                    label = "CONTINUOUS_SCANNING_ADD_UNSTEALTH_LABEL"
                    NoStringtableLookup
                    icon = "icons/species/hhhoh.png"
                    parameters = [
                        tag = "empire" data = Source.Owner
                        tag = "ship"   data = Target.ID
                        tag = "planet" data = Target.ID
                        tag = "unst"   data = Value(Source.Unstealth)
                        tag = "crunst" data = Source.Unstealth
                    ]
                    affiliation = AnyEmpire
            ]

        // increase ship detection range
        EffectsGroup
            scope = And [ Ship OwnedBy empire = Source.Owner ]
            activation = Source
            effects = SetDetection value = Value + min((NamedReal name = "CONTINUOUS_SCANNING_RANGE_BOOST" value = 10.0),
                                                       TurnsSincePolicyAdopted empire = Source.Owner name = ThisPolicy)

    ]
    graphic = "icons/policies/military_continuous_scanning.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/common/priorities.macros"


BuildingType
    name = "BLD_INTERSPECIES_ACADEMY"
    description = "BLD_INTERSPECIES_ACADEMY_DESC"
    buildcost = 100
    buildtime = 1
    location = AND [
        // Homeworld
        Species
        Not Contains Building name = "BLD_INTERSPECIES_ACADEMY"
        OwnedBy empire = Source.Owner
        Happiness low = 20
        Not WithinStarlaneJumps jumps = 3 condition = And [
            System
            Contains And [
                Building name = "BLD_INTERSPECIES_ACADEMY"
                OwnedBy empire = Source.Owner
            ]
        ]
        Number low = 0 high = 0 condition = And [
            Building name = "BLD_INTERSPECIES_ACADEMY"
            Species name = RootCandidate.Species
            OwnedBy empire = RootCandidate.Owner
        ]
        Number low = 0 high = 0 condition = Described description = "Academies of this species already exist in the empire" condition = And [
            Planet
            Enqueued type = Building name = "BLD_INTERSPECIES_ACADEMY"
            Species name = RootCandidate.Species
            OwnedBy empire = RootCandidate.Owner
            Not Object id = RootCandidate.PlanetID
        ]
        Number low = 0 high = 6 condition = And [
            Building name = "BLD_INTERSPECIES_ACADEMY"
            OwnedBy empire = Source.Owner
        ]
        
    ]
    EnqueueLocation = [[ENQUEUE_BUILD_ONE_PER_PLANET]]
    effectsgroups = [
        EffectsGroup 
            scope = NumberOf number = 1 condition = And [
                Building name = "BLD_INTERSPECIES_ACADEMY"
                OwnedBy empire = Source.Owner
                Not Object id = Source.ID
                Species name = Source.Planet.Species
            ]
            activation = And [
                Source
                Random probability = 0.3
            ]
            effects = [
                GenerateSitRepMessage
                    message = "Destroying fake duplicate design academy %planet%."
                    label = "EFFECT_BLACKHOLE_LABEL"
                    NoStringtableLookup
                    icon = "icons/building/blackhole.png"
                    parameters = [
                        tag = "planet" data = Target.PlanetID
                    ]
                    empire = Source.Owner
                Destroy
            ]
            
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
                OwnedBy empire = Source.Owner
                Species
            ]
            activation = Source
            priority = [[LATE_PRIORITY]]
            effects = [
                GenerateSitRepMessage
                    message = "Additional effect by the %species% on %planet% joining the design academy. Each design academy adds 0.05 to %rawtext%"
                    label = "EFFECT_BLACKHOLE_LABEL"
                    NoStringtableLookup
                    icon = "icons/building/blackhole.png"
                    parameters = [
                        tag = "planet" data = Source.PlanetID
                        tag = "species" data = Source.Planet.Species
                        tag = "rawtext" data = EmpireMeterValue empire = Source.Owner meter = "METER_IMPERIAL_PP_TRANSFER_EFFICIENCY"
                    ]
                    empire = Source.Owner
                SetEmpireMeter empire = Source.Owner meter = "METER_IMPERIAL_PP_TRANSFER_EFFICIENCY" value = Value + 0.05
            ]
    ]
    icon = "icons/building/science-institute.png"

#include "/scripting/common/base_prod.macros"

#include "/scripting/common/enqueue.macros"
#include "/scripting/common/priorities.macros"

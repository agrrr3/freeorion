BuildingType
    name = "BLD_COLONY_INDEPENDENCE_DECREE"
    description = "BLD_COLONY_INDEPENDENCE_DECREE_DESC"
    buildcost = 20
    buildtime = 1
    location = And [
        Planet
        OwnedBy empire = Source.Owner
        Population
    ]

    effectsgroups = [
         // TODO: implement SetFocus effect
         // switch independent colony focus to defense so the independent colony can have the highest defense values
         //EffectsGroup
         //   scope = And [
         //       Object id = Source.PlanetID
         //       Planet
         //   ]
         //   activation = Location type=Focus name = Source.Species name = "FOCUS_DEFENSE"
         //   priority = [[BEFORE_ANYTHING_ELSE_PRIORITY]]
         //   effects = SetFocus name = "FOCUS_DEFENSE"

         // make colony independent (unowned)
         EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
            ]
            activation = Source
            priority = [[COLONY_INDEPENDENCE_PRIORITY]]
            effects = [
                SetSpecialCapacity name = "INDEPENDENT_COLONY_SHIELD_SPECIAL" capacity = Value(Target.MaxShield)
                SetSpecialCapacity name = "INDEPENDENT_COLONY_DEFENSE_SPECIAL" capacity = Value(Target.MaxDefense)
                SetSpecialCapacity name = "INDEPENDENT_COLONY_TROOPS_SPECIAL" capacity = Value(Target.MaxTroops)
                SetOwner empire = [[UNOWNED_EMPIRE_ID]]
                GenerateSitRepMessage
                    message = "SITREP_COLONY_INDEPENDENT"
                    label = "SITREP_COLONY_INDEPENDENT_LABEL"
                    icon = "icons/sitrep/planet_captured.png"
                    parameters =  [
                        tag = "planet" data = Target.ID
                        tag = "species" data = Target.Species
                    ]
                    empire = Source.Owner
            ]

         EffectsGroup
            scope = Source
            priority = [[END_CLEANUP_PRIORITY]]
            effects = Destroy
    ]
    icon = "icons/sitrep/planet_captured.png"

#include "/scripting/common/priorities.macros"
#include "/scripting/common/misc.macros"
